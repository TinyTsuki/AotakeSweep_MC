plugins {
    id 'fabric-loom' version '1.15-SNAPSHOT'
    id 'maven-publish'
    // 若以上行报错则大概率为网络问题, 请切换网络并刷新Gradle
}

def javaVer = 17

version = project.mod_version
mod_id = project.mod_id
mod_name = project.mod_name
mod_desc = project.mod_desc

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(javaVer)
}

loom {
    runs {
        client {
            programArgs '--username', 'Dev', '--uuid', '380df991-f603-344c-a090-369bad2a924a'
        }
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    // flatDir {
    //     dir 'libs'
    // }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.terraformersmc.com/releases" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-networking-api-v1:${project.fabric_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-lifecycle-events-v1:${project.fabric_version}"

    dependencies {
        modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
    }

    dependencies {
        modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")
    }

    testImplementation 'junit:junit:4.13.2'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "org.apache.maven:maven-artifact:4.0.0-rc-3"

}

processResources {
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_name", project.mod_name
    inputs.property "mod_desc", project.mod_desc
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "mod_id": mod_id,
                "mod_name": project.mod_name,
                "mod_desc": project.mod_desc,
                "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (javaVer >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(javaVer)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(javaVer)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(javaVer)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

tasks.named('jar', Jar).configure {
    from("LICENSE") {
        rename { "${it}_${project.mod_id}" }
    }

    manifest {
        attributes([
            "Specification-Title"     : mod_id,
            "Specification-Vendor"    : mod_authors,
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : project.version,
            "Implementation-Vendor"   : mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.named('remapJar') {
    archiveBaseName.set(mod_id)
    archiveVersion.set("fabric-${project.mod_version}")
    destinationDirectory.set(file("${projectDir}/builds/${project.mod_version.split('-')[1]}"))
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.mod_id
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
